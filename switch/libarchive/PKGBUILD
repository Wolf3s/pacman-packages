# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-libarchive
pkgver=3.7.2
pkgrel=6
pkgdesc='library that can create and read several streaming archive formats'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-3.7.2.patch"
)

makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=('switch-bzip2' 'switch-zlib' 'switch-liblzma' 'switch-lz4' 'switch-libexpat' 'switch-libzstd')
groups=('switch-portlibs')

build() {
  cd libarchive-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  CFLAGS="${CFLAGS} -DHAVE_ARC4RANDOM_BUF" \
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static \
    --disable-bsdtar --disable-bsdcat \
    --disable-bsdunzip --disable-bsdcpio --disable-acl \
    --without-xml2 --without-openssl

  make
}

package() {
  cd libarchive-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 "$srcdir"/libarchive-${pkgver}/COPYING  "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/bin
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

sha256sums=('df404eb7222cf30b4f8f93828677890a2986b66ff8bf39dac32a804e96ddf104'
            '975c46e9ce765407fb369ec369a2e7f746801f801238a830a38ec61676929fa3')
