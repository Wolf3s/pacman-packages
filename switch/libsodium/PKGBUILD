# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: rsn8887 <raist66676@gmx.de>

pkgname=switch-libsodium
pkgver=1.0.20
pkgrel=1
pkgdesc='A modern and easy-to-use crypto library.'
arch=('any')
url='http://libsodium.org'
license=('custom')
options=(!strip libtool staticlibs)
makedepends=('dkp-toolchain-vars')
source=("https://download.libsodium.org/libsodium/releases/libsodium-$pkgver-stable.tar.gz")
groups=('switch-portlibs')

build() {
  cd libsodium-stable

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static
  make
}

package() {
  cd libsodium-stable

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
  
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
}
sha256sums=('e6d3c91bbc08d3f5f548079bd88abd9e51de9c5b48b3f8b1d120b2160e6537d0')
