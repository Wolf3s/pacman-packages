# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>

pkgname=switch-libsmb2
pkgver=3.0.0
pkgrel=2
pkgdesc='SMB2/3 userspace client'
arch=('any')
url='https://github.com/sahlberg/libsmb2'
license=('(L)GPL')
options=(!strip libtool staticlibs)
source=("https://github.com/sahlberg/libsmb2/archive/v${pkgver}.tar.gz")
sha256sums=('36d10d1628ad34d56646bf9e7005a8abe8ce6b67ba3a949d537ae3e5f08f701f')
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')

prepare() {
  cd .
}

build() {
  cd libsmb2-$pkgver

  source /opt/devkitpro/switchvars.sh

  libtoolize
  aclocal
  autoheader
  automake --add-missing
  autoconf

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --without-libkrb5

  #cp config.h /lib

  make
}

package() {
  cd libsmb2-$pkgver

  make DESTDIR="$pkgdir" install
}
