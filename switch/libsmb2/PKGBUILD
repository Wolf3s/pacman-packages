# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>

pkgname=switch-libsmb2
pkgver=r285.91d6c8a
pkgrel=1
pkgdesc='SMB2/3 userspace client'
arch=('any')
url='https://github.com/sahlberg/libsmb2'
license=('(L)GPL')
options=(!strip libtool staticlibs)
source=(${pkgname}::"git+https://github.com/sahlberg/libsmb2.git#commit=91d6c8a44b6f6adaa879cc556cf96ff6077bbb4f")
sha256sums=('SKIP')
makedepends=('git' 'switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')

pkgver() {
  cd "$srcdir/${pkgname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname}
  patch -Np1 -i "$srcdir/../libsmb2-91d6c8a-define_endian_macros.patch"
  patch -Np1 -i "$srcdir/../libsmb2-91d6c8a-define_getlogin_r.patch"
  patch -Np1 -i "$srcdir/../libsmb2-91d6c8a-fix_portable_endian_include.patch"
}

build() {
  cd ${pkgname}

  source /opt/devkitpro/switchvars.sh

  libtoolize
  aclocal
  autoheader
  automake --add-missing
  autoconf

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --without-libkrb5

  make
}

package() {
  cd ${pkgname}
  
  make DESTDIR="$pkgdir" install
  
  install -Dm644 LICENCE "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/LICENCE-LGPL-2.1.txt
  rm -r "$pkgdir"/opt/devkitpro/portlibs/switch/share
}
